import org.jlleitschuh.gradle.ktlint.reporter.ReporterType
apply plugin: 'com.github.ben-manes.versions'

apply from: "gradle/dependencies.gradle"
buildscript {


    repositories {
        mavenCentral()
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.2.0'
        classpath 'com.google.gms:google-services:4.3.4'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.33.0'
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.6.2.0"

        // https://github.com/JLLeitschuh/ktlint-gradle
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.2.1"

        https://github.com/jmatsu/license-list-plugin
        classpath "io.github.jmatsu:license-list-gradle:0.5.0"

        classpath "org.koin:koin-gradle-plugin:$koinVersion"
    }
}

apply plugin: 'koin'

def version_code = System.getenv("VERSION_CODE") ?: "1"
def version_name = System.getenv("VERSION_NAME") ?: "1000.${version_code}"

ext.versions = [
        compileSdk : 30,
        buildTools : '30.0.0',
        targetSdk  : 29,
        minSdk     : 21,
        versionCode: Integer.parseInt(version_code),
        versionName: version_name
]
ext {
    jacocoToolVersion = "0.8.5"
}


allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://adgeneration.github.io/ADG-Android-SDK/repository' }
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    ktlint {
        version = "0.36.0"
        android = true
        ignoreFailures = true

        reporters {
            reporter "checkstyle"
        }
    }
}
